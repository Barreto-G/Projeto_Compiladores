<PROGRAMA> ::= {<DEFINICAO-DE-FUNCAO>} <PRINCIPAL> {<DEFINICAO-DE-FUNCAO>}

<DEFINICAO-DE-FUNCAO> ::= <ESPECIFICADOR-DE-TIPO> <IDENTIFICADOR> "(" [<LISTA-DE-PARAMETROS>] ")" <DECLARACAO-COMPOSTA>

<PRINCIPAL> ::= "INTEIRO" "PRINCIPAL" "(" [<LISTA-DE-PARAMETROS>] ")" <DECLARACAO-COMPOSTA>

<ESPECIFICADOR-DE-TIPO> ::= "INTEIRO" | "FLUTUANTE" | "CARACTERE" | "SIMOUNAO" | "VAZIO"

<LISTA-DE-PARAMETROS> ::= <PARAMETRO> "," <LISTA-DE-PARAMETROS> | <PARAMETRO> 

<PARAMETRO> ::= <ESPECIFICADOR-DE-TIPO> <IDENTIFICADOR>

<DECLARACAO-COMPOSTA> ::= "{" <LISTA-DE-DECLARACAO> "}"

<LISTA-DE-DECLARACAO> ::= <DECLARACAO> <LISTA-DE-DECLARACAO> | <DECLARACAO>

<DECLARACAO> ::= <DECLARACAO-DE-EXPRESSAO> | <DECLARACAO-COMPOSTA> | <DECLARACAO-DE-SELECAO> | <DECLARACAO-DE-ITERACAO> | <DECLARACAO-DE-RETORNO> | <DECLARACAO-DE-DECLARACAO>

<DECLARACAO-DE-EXPRESSAO> ::= <EXPRESSAO-DE-ATRIBUICAO> ";"

<DECLARACAO-DE-SELECAO> ::= "SE" "(" <EXPRESSAO-DE-ATRIBUICAO> ")" <DECLARACAO> [ "SENAO" <DECLARACAO> ]

<DECLARACAO-DE-ITERACAO> ::= "ENQUANTO" "(" <EXPRESSAO-DE-ATRIBUICAO> ")" <DECLARACAO> | "FACA" <DECLARACAO> "ENQUANTO" "(" <EXPRESSAO-DE-ATRIBUICAO> ")" ";" | "PARA" "(" <DECLARACAO-DE-EXPRESSAO> <EXPRESSAO-DE-ATRIBUICAO> ";" <EXPRESSAO-DE-ATRIBUICAO> ")" <DECLARACAO>

<DECLARACAO-DE-RETORNO> ::= "RETORNA" [ <EXPRESSAO-DE-ATRIBUICAO> ] ";"

<DECLARACAO-DE-DECLARACAO> ::= <DECLARACAO> ";"

<EXPRESSAO-DE-ATRIBUICAO> ::= <EXPRESSAO-LOGICA-OU> | <EXPRESSAO-UNITARIA> "IGUAL" <EXPRESSAO-DE-ATRIBUICAO>

<EXPRESSAO-LOGICA-OU> ::= <EXPRESSAO-LOGICA-E> | <EXPRESSAO-LOGICA-OU> "OU" <EXPRESSAO-LOGICA-E>

<EXPRESSAO-LOGICA-E> ::= <EXPRESSAO-DE-IGUALDADE> | <EXPRESSAO-LOGICA-E> "E" <EXPRESSAO-DE-IGUALDADE>

<EXPRESSAO-DE-IGUALDADE> ::= <EXPRESSAO-RELACIONAL> | <EXPRESSAO-DE-IGUALDADE> "COMPARADO" <EXPRESSAO-RELACIONAL> | <EXPRESSAO-DE-IGUALDADE> "DIFERENTE" <EXPRESSAO-RELACIONAL>

<EXPRESSAO-RELACIONAL> ::= <EXPRESSAO-ADITIVA> | <EXPRESSAO-RELACIONAL> "MENOR" <EXPRESSAO-ADITIVA> | <EXPRESSAO-RELACIONAL> "MAIOR" <EXPRESSAO-ADITIVA> | <EXPRESSAO-RELACIONAL> "MENOROUIGUAL" <EXPRESSAO-ADITIVA> | <EXPRESSAO-RELACIONAL> "MAIOROUIGUAL" <EXPRESSAO-ADITIVA>

<EXPRESSAO-ADITIVA> ::= <EXPRESSAO-MULTIPLICATIVA> | <EXPRESSAO-ADITIVA> "MAIS" <EXPRESSAO-MULTIPLICATIVA> | <EXPRESSAO-ADITIVA> "MENOS" <EXPRESSAO-MULTIPLICATIVA>

<EXPRESSAO-MULTIPLICATIVA> ::= <EXPRESSAO-UNITARIA> | <EXPRESSAO-MULTIPLICATIVA> "VEZES" <EXPRESSAO-UNITARIA> | <EXPRESSAO-MULTIPLICATIVA> "DIVIDIDO" <EXPRESSAO-UNITARIA>

<EXPRESSAO-UNITARIA> ::= <EXPRESSAO-POSFIXA> | <OPERADOR-UNITARIO> <EXPRESSAO-UNITARIA>

<EXPRESSAO-POSFIXA> ::= <EXPRESSAO-PRIMARIA> | <EXPRESSAO-POSFIXA> "[" <EXPRESSAO-DE-ATRIBUICAO> "]" | <EXPRESSAO-POSFIXA> "(" [ <LISTA-DE-EXPRESSAO-DE-ARGUMENTO> ] ")" | <EXPRESSAO-POSFIXA> "." <IDENTIFICADOR>

<LISTA-DE-EXPRESSAO-DE-ARGUMENTO> ::= <EXPRESSAO-DE-ATRIBUICAO> | <EXPRESSAO-DE-ATRIBUICAO> "," <LISTA-DE-EXPRESSAO-DE-ARGUMENTO>

<EXPRESSAO-PRIMARIA> ::= <IDENTIFICADOR> | <CONSTANTE> | "(" <EXPRESSAO-DE-ATRIBUICAO> ")"

<CONSTANTE> ::= <INTEIRO> | <FLUTUANTE> | <CARACTERE> | <SIMOUNAO>

<IDENTIFICADOR> ::= <LETRA> { <LETRA> | <DIGITO> }

<INTEIRO> ::= <DIGITO> { <DIGITO> }

<FLUTUANTE> ::= <DIGITO> "." <DIGITO> { <DIGITO> }

<CARACTERE> ::= "'" <SIMBOLO> "'"

<SIMOUNAO> ::= "VERDADEIRO" | "FALSO"

<SIMBOLO> ::= <LETRA> | <DIGITO> | "(" | ")" | "{" | "}" | "[" | "]"

<LETRA> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"

<DIGITO> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" 

